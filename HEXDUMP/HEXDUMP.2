<DEFINE PRINT-HEX (WORD)
   <REPEAT ((BIT 32) NUM)
     <SET NUM <CHTYPE <GETBITS .WORD <BITS 4 .BIT>> FIX>>
     <COND (<L? .NUM 10> <PRIN1 .NUM>)
           (<=? .NUM 10> <PRINC "A">)
           (<=? .NUM 11> <PRINC "B">)
           (<=? .NUM 12> <PRINC "C">)
           (<=? .NUM 13> <PRINC "D">)
           (<=? .NUM 14> <PRINC "E">)
           (<=? .NUM 15> <PRINC "F">)>
     <SET BIT <- .BIT 4>>
     <COND (<L? .BIT 0> <PRINC " "> <RETURN>)>>>

<DEFINE HEXDUMP-EXPORT (FILE "AUX" (BUF <UVECTOR 1>) (CNT 0) CHN)
   <SET CHN <OPEN "READB" .FILE>>
   <COND (.CHN ;"If CHN is FALSE, bad OPEN: return the FALSE"
     <CRLF>
     <REPEAT ((NUM 0))
       <SET NUM <READB .BUF .CHN '<RETURN>>>
       <COND (<0? .NUM> <RETURN>)>
       <PRINT-HEX <GET .BUF 1>>
       <SET CNT <+ 1 .CNT>>
       <COND (<==? 0 <MOD .CNT 6>> <CRLF>)>>
    <PRINC "END OF FILE"> <CRLF>
    <CLOSE .CHN>)>>

<DEFINE HEXDUMP-IMPORT (FILE "AUX" (WORD 0) CHR (CNT 0) (BUF <UVECTOR 1>) CHN OUT NUM (POS 1))
  <SET CHN <OPEN "READ" .FILE>>
  <SET OUT <OPEN "PRINTB" "HEASM;TEST OUT">>
  <COND (.CHN ;"If CHN is FALSE, bad OPEN: return the FALSE"
    <REPEAT ()
      <SET CHR <READCHR .CHN '<RETURN>>>
      <SET NUM <ASCII .CHR>>
      <COND (<OR <AND <G=? .NUM 65> <L=? .NUM 70>>
                 <AND <G=? .NUM 48> <L=? .NUM 57>>>
        <COND (<G=? .NUM 65> <SET NUM <- .NUM 55>>)
              (ELSE <SET NUM <- .NUM 48>>)>
        <SET WORD <PUTBITS .WORD <BITS 4 <* <- 9 .POS> 4>> .NUM>> 
        <SET POS <+ 1 .POS>>
        <COND (<==? .POS 10> 
          <SET POS 1>
          <PRIN1 .CNT> <PRINC ": "> <PRIN1 <CHTYPE .WORD WORD>>
          <CRLF>
          <PUT .BUF 1 .WORD>
          <PRINTB .BUF .OUT>
          <SET CNT <+ 1 .CNT>>)>)>>
  <PRINC "END OF FILE"> <CRLF>
  <CLOSE .CHN>
  <CLOSE .OUT>)>>
