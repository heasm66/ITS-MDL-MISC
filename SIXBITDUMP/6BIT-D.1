"******************************************************************
 * SIXBIT-EXPORT Print content of specified file as sixbit to     *
 *               screen. Each WORD (36-bits) is 6 chars, 10 WORDs *
 *               per line. Chars is between 33 and 96. 32 (SPACE) *
 *               is excluded for readability.                     *
 * SIXBIT-IMPORT  Converts file, saved from SIXBIT-EXPORT, back   *
 *                into original file.                             *
 *                                                                *
 * 1. SIXBIT-EXPORT file and copy from screen into external file  *
 * 2. Paste hex-chars into an emacs file:                         *
 *         :EMACS <DIR>;<NAME1> <NAME2>                           *
 *         CTRL-X, CTRL-S to save file.                           *
 * 3. SIXBIT-IMPORT emacs-file to recreate original file.         *
 * 4. :CRC can be used to check recreated file against original.  *
 *                                                                *
 * (Note: Only paste about 2000 lines at a time into EMACS before *
 *        saving, then paste additional lines.)                   *
 *                                                                *
 * If content doesn't fit into one file, modify SIXBIT-IMPORT to  *
 * import and merge from multiple source files.                   *
 ******************************************************************"
<DEFINE PRINT-SIXBIT (WORD)
   <REPEAT ((BIT 30))
     <PRINC <ASCII <+ <CHTYPE <GETBITS .WORD <BITS 6 .BIT>> FIX> 33>>>
     <SET BIT <- .BIT 6>>
     <COND (<L? .BIT 0> <RETURN>)>>>

<DEFINE SIXBIT-EXPORT (FILE "AUX" (BUF <UVECTOR 1>) (CNT 0) CHN)
   <SET CHN <OPEN "READB" .FILE>>
   <COND (.CHN ;"If CHN is FALSE, bad OPEN: return the FALSE"
     <PRINC "START OF FILE"> <CRLF>
     <REPEAT ((NUM 0))
       <SET NUM <READB .BUF .CHN '<RETURN>>>
       <COND (<0? .NUM> <RETURN>)>
       <PRINT-SIXBIT <GET .BUF 1>>
       <SET CNT <+ 1 .CNT>>
       <COND (<0? <MOD .CNT 10>> <CRLF>)>>
    <PRINC "END OF FILE"> <CRLF>
    <CLOSE .CHN>)>>

<DEFINE SIXBIT-IMPORT (INFILE OUTFILE 
    "AUX" (WORD 0) CHR (CNT 0) (BUF <UVECTOR 1>) 
	INCHN OUTCHN NUM (POS 1))
  <SET OUTCHN <OPEN "PRINTB" .OUTFILE>>
  ;"============================================
    = Repeat this section if you want to merge =
    = multiple source files into one outfile.  =
    ============================================"
  <SET INCHN <OPEN "READ" .INFILE>>
  <COND (.INCHN ;"If CHN is FALSE, bad OPEN: return the FALSE"
    <REPEAT ()
      <SET CHR <READCHR .INCHN '<RETURN>>>
      <SET NUM <ASCII .CHR>>
      <COND (<AND <G=? .NUM 33> <L=? .NUM 96>>
        <SET WORD <PUTBITS .WORD <BITS 6 <* <- 6 .POS> 6>> <- .NUM 33>>> 
        <SET POS <+ 1 .POS>>
        <COND (<==? .POS 7> 
          <SET POS 1>
          <COND (<=? <MOD .CNT 1000> 0>
            <COND (<0? .CNT> <PRINC "STARTING...">)
                  (ELSE <PRIN1 .CNT> <PRINC "...">)>)>
          <PUT .BUF 1 .WORD>
          <PRINTB .BUF .OUTCHN>
          <SET CNT <+ 1 .CNT>>)>)>>
  <CLOSE .INCHN>)>
  ;"============================================"
  <PRINC "END OF FILE"> <CRLF>
  <CLOSE .OUTCHN>>
  

